#
# mpabegg prompt theme for zprezto
#

# Load dependencies.
pmodload 'helper'

function prompt_mpabegg_pwd {
  # local pwd="${PWD/#$HOME/~}"
  #
  # if [[ "$pwd" == (#m)[/~] ]]; then
  #   _prompt_mpabegg_pwd="$MATCH"
  #   unset MATCH
  # else
  #   echo "NOT MATCH => $pwd"
  #   _prompt_mpabegg_pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
  # fi

  _prompt_mpabegg_pwd="${PWD/#$HOME/~}"
}

function prompt_mpabegg_should_run_git_info {
  local gitignore{,s} gi pwd

  zstyle -a ':prezto:module:prompt:mpabegg' gitignore 'gitignores'
  for gitignore ("$gitignores[@]")
  do
    gi=${${~gitignore}%%/#}/
    pwd=${PWD}/
    if (( ${#${pwd#${gi}}} < ${#pwd} ))
    then
      return 1
    fi
  done
}

function prompt_mpabegg_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Format PWD.
  prompt_mpabegg_pwd

  # Get Git repository information.
  if prompt_mpabegg_should_run_git_info && (( $+functions[git-info] )); then
    git-info
  else
    git_info=
  fi
}

function prompt_mpabegg_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_mpabegg_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format ' %B%F{red}❯%F{yellow}❯%F{green}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format ' %B%F{green}❮%F{yellow}❮%F{red}❮%f%b'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '%%B%F{yellow}%s%f%%b'
  zstyle ':prezto:module:git:info:added' format ' %%B%F{green}✚%f%%b'
  zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}⬆%f%%b'
  zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}⬇%f%%b'
  zstyle ':prezto:module:git:info:branch' format '%F{green}%b%f'
  zstyle ':prezto:module:git:info:commit' format '%F{green}%.7c%f'
  zstyle ':prezto:module:git:info:deleted' format ' %%B%F{red}✖%f%%b'
  zstyle ':prezto:module:git:info:modified' format ' %%B%F{blue}✱%f%%b'
  zstyle ':prezto:module:git:info:position' format '%F{red}%p%f'
  zstyle ':prezto:module:git:info:renamed' format ' %%B%F{magenta}➜%f%%b'
  zstyle ':prezto:module:git:info:stashed' format ' %%B%F{cyan}✭%f%%b'
  zstyle ':prezto:module:git:info:unmerged' format ' %%B%F{yellow}═%f%%b'
  zstyle ':prezto:module:git:info:untracked' format ' %%B%F{white}?%f%%b'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '%F{yellow}(%f$(coalesce "%b" "%p" "%c")%A%B%S%a%d%m%r%U%u%C%F{yellow})%f' \
    # 'rprompt' '%A%B%S%a%d%m%r%U%u'

  # Define prompts.
  PROMPT='
%F{red}${_prompt_mpabegg_pwd}%f ${git_info:+${(e)git_info[prompt]}}
%(!. %B%F{red}#%f%b.)${editor_info[keymap]} '
  RPROMPT='${editor_info[overwrite]}%(?:: %F{red}⏎%f)${VIM:+" %F{green}V%f"}[%T]'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_mpabegg_setup "$@"
